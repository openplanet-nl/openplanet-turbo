#Include "TextLib" as TL

#Include "Libs/Nadeo/TMConsole/Logger.Script.txt"					as Logger
#Include "Libs/Nadeo/TMConsole/PopUpManager.Script.txt"				as PopUpManager


declare Boolean	G_RecordCenterMode;
declare Boolean G_StatSent;

Void WarnNotEnoughParams(CMlAppEvent _Event) {
	Logger::Warn("[Menucontroller] event "^_Event.CustomEventType^": not enough params ["^_Event.CustomEventData^"]");
}

Boolean Get_RecordCenterMode()
{
	if(G_RecordCenterMode)
	{
		G_RecordCenterMode = False;
		return True;
	}
	return False;
}

Void ReleaseRecordsTaskResult(Text PageLoaded)
{
	if(PageLoaded == "Main")
	{
		declare CUILayer Layer_RecordCenter for This;
		declare CWebServicesTaskResult 	RecordsTaskResult 		for Layer_RecordCenter.LocalPage;
		declare Boolean 				RecordsTaskResultLoaded for Layer_RecordCenter.LocalPage;

		if(RecordsTaskResult != Null) {
			ManiaPlanet.MasterServer_ReleaseMSTaskResult(RecordsTaskResult.Id);
		}
		
		RecordsTaskResultLoaded = False;
	}
	else
	{
		declare CUILayer Layer_RecordCenterDetails for This;
		declare CWebServicesTaskResult	RecordsTaskResult		for Layer_RecordCenterDetails.LocalPage;
		declare Boolean					RecordsTaskResultLoaded for Layer_RecordCenterDetails.LocalPage;

		if(RecordsTaskResult != Null) {
			ManiaPlanet.MasterServer_ReleaseMSTaskResult(RecordsTaskResult.Id);
		}
		
		RecordsTaskResultLoaded = False;
	}
}

Void GetRecordsResume(Text SelectedFilter, Text FromOutside)
{
	declare CUILayer 				Layer_RecordCenter 		for This;
	declare CWebServicesTaskResult 	RecordsTaskResult 		for Layer_RecordCenter.LocalPage;
	declare Boolean					RecordsTaskResultLoaded for Layer_RecordCenter.LocalPage;
	declare Text[Text][] 			RequestedRecordsResume 	for Layer_RecordCenter.LocalPage;

	// if(RecordsTaskResultLoaded == False) LoadRecordsTaskResult();
	declare BuddiesComparisonResult <=> (RecordsTaskResult as CWebServicesTaskResult_BuddiesChallengeRecordsComparison);

	switch(SelectedFilter)
	{
		case "1": BuddiesComparisonResult.SortByOpponentDisplayName();
		case "2": BuddiesComparisonResult.SortByPlayerCount();
		case "3": BuddiesComparisonResult.SortByOpponentCount();
		case "4": BuddiesComparisonResult.SortByOpponentDate();
	}

	declare Integer BetterCount	= 0;
	declare Integer EqualCount 	= 0;
	declare Integer WorseCount 	= 0;
	
	// Load the data to display in the table
	RequestedRecordsResume.clear ();
	foreach(Buddy in BuddiesComparisonResult.BuddiesComparison) 
	{
		RequestedRecordsResume.add([
		"OpponentLogin" 					=> Buddy.OpponentLogin,
		"OpponentDisplayName"				=> Buddy.OpponentDisplayName,
		"OpponentBestRecordCount" 			=> TL::ToText(Buddy.OpponentBestRecordCount),
		"BestRecordCount" 					=> TL::ToText(Buddy.BestRecordCount),
		"OpponentBestRecordElapsedTime" 	=> ""^Buddy.OpponentBestRecordElapsedTime
		]);
		
		if(Buddy.OpponentBestRecordCount > Buddy.BestRecordCount) 		WorseCount 	+= 1;
		else if(Buddy.OpponentBestRecordCount < Buddy.BestRecordCount) 	BetterCount += 1;
		else															EqualCount 	+= 1;
	}

	if(! G_StatSent) {
		G_StatSent = True;
		ManiaPlanet.MasterServer_Tracking_StatRecordCenter(DataMgr.MenuUserId, BetterCount, EqualCount, WorseCount);
		log(Now^"> MasterServer_Tracking_StatRecordCenter: "^BetterCount^", "^EqualCount^", "^WorseCount);
	}
	
	if(FromOutside != "True") LayerCustomEvent(Layer_RecordCenter, "RecordsLoaded", [""]);
}

Void GetRecords(Text SelectedFilter, Text BuddyLogin, Text SelectedGroup, Text FromOutside, Text BuddyName){
	declare CUILayer Layer_RecordCenterDetails for This;

	declare Text[Text][] 			RemotePlayerRecords;
	declare Text[Text][]		 	RemoteBuddyRecords;
	declare Text[Text][] 			RequestedPlayerRecords 	for Layer_RecordCenterDetails.LocalPage;
	declare CWebServicesTaskResult	RecordsTaskResult 			for Layer_RecordCenterDetails.LocalPage;
	declare Boolean 				RecordsTaskResultLoaded for Layer_RecordCenterDetails.LocalPage;
	declare Text[Text][] 			RequestedBuddyRecords 	for Layer_RecordCenterDetails.LocalPage;

	// if(RecordsTaskResultLoaded == False) LoadRecordsTaskResult(BuddyLogin);
	// wait(RecordsTaskResultLoaded == True);
	declare RecordsComparisonResult <=> (RecordsTaskResult as CWebServicesTaskResult_BuddyChallengeRecordsComparison);

	switch(SelectedFilter){
		case "1": RecordsComparisonResult.SortByMapName();
		case "2": RecordsComparisonResult.SortByRecordTime();
		case "3": RecordsComparisonResult.SortByRecordDate();
	}

	// Check if the player wants to load the buddy data
	if(SelectedGroup == "0"){
		RemoteBuddyRecords.clear();

    foreach(Record in RecordsComparisonResult.BuddyBestRecordsComparison){
			RemoteBuddyRecords.add(["Mapname" => Record.MapInfo.Name,
			"MapFileName" => Record.MapInfo.FileName,
			"OpponentRecordTime" => TL::TimeToText(Record.OpponentRecordTime, True),
			"RecordTimeDiff" => TL::TimeToText(Record.RecordTime - Record.OpponentRecordTime),
			"OpponentRecordDate" => TL::ToText(Record.OpponentRecordDate),
			"OpponentRecordElapsedTime" => TL::ToText(Record.OpponentRecordElapsedTime),
			"GhostUrl" => Record.OpponentRecordUrl]);
    }

		RequestedBuddyRecords = RemoteBuddyRecords;
	} else if(SelectedGroup == "1"){
		RemotePlayerRecords.clear();

		foreach(Record in RecordsComparisonResult.PlayerBestRecordsComparison){
			RemotePlayerRecords.add(["Mapname" => Record.MapInfo.Name,
			"MapFileName" => Record.MapInfo.FileName,
			"RecordTime" => TL::TimeToText(Record.RecordTime, True),
			"RecordTimeDiff" => TL::TimeToText(Record.OpponentRecordTime - Record.RecordTime, True),
			"RecordDate" => TL::ToText(Record.RecordDate),
			"RecordElapsedTime" => TL::ToText(Record.RecordElapsedTime),
			"GhostUrl" => Record.OpponentRecordUrl]);
    }

		RequestedPlayerRecords = RemotePlayerRecords;
	} else { // The player wants to load the data for him and his buddy (come from another page)
		RemotePlayerRecords.clear();
		RemoteBuddyRecords.clear();

		foreach(Record in RecordsComparisonResult.BuddyBestRecordsComparison){
			RemoteBuddyRecords.add(["Mapname" => Record.MapInfo.Name,
			"MapFileName" => Record.MapInfo.FileName,
			"RecordTime" => TL::TimeToText(Record.OpponentRecordTime, True),
			"RecordTimeDiff" => TL::TimeToText(Record.RecordTime - Record.OpponentRecordTime, True),
			"RecordDate" => TL::ToText(Record.OpponentRecordDate),
			"RecordElapsedTime" => TL::ToText(Record.OpponentRecordElapsedTime),
			"GhostUrl" => Record.OpponentRecordUrl]);
		}

		foreach(Record in RecordsComparisonResult.PlayerBestRecordsComparison){
			RemotePlayerRecords.add(["Mapname" => Record.MapInfo.Name,
			"MapFileName" => Record.MapInfo.FileName,
			"RecordTime" => TL::TimeToText(Record.RecordTime, True),
			"RecordTimeDiff" => TL::TimeToText(Record.OpponentRecordTime - Record.RecordTime, True),
			"RecordDate" => TL::ToText(Record.RecordDate),
			"RecordElapsedTime" => TL::ToText(Record.RecordElapsedTime),
			"GhostUrl" => Record.OpponentRecordUrl]);
		}

		RequestedPlayerRecords = RemotePlayerRecords;
		RequestedBuddyRecords = RemoteBuddyRecords;

		if(FromOutside != "True") LayerCustomEvent(Layer_RecordCenterDetails, "BuddyLoaded", [BuddyLogin, SelectedFilter, BuddyName]);
	}
}

Void LoadRecordsTaskResult(Text SelectedFilter, Text FromOutside)
{
	declare CUILayer Layer_RecordCenter for This;

	declare CWebServicesTaskResult 	RecordsTaskResult 		for Layer_RecordCenter.LocalPage;
	// declare Boolean 				RecordsTaskResultLoaded for Layer_RecordCenter.LocalPage;

	// RecordsTaskResult = ManiaPlanet.MasterServer_GetBuddiesChallengeRecordsComparison( DataMgr.MenuUserId );
	
	//CB: on release d'abord la tche prcdente, sinon a stack
	if(RecordsTaskResult != Null) {
		ManiaPlanet.MasterServer_ReleaseMSTaskResult(RecordsTaskResult.Id);
		log("RELEASE TASK BUDDIES COMPARISON");
	}
	
	
	RecordsTaskResult = ScoreMgr.Campaign_GetBuddiesMapRecordsComparison( DataMgr.MenuUserId );

	declare Time = Now;
	wait(!RecordsTaskResult.IsProcessing || Now >= Time + 5 * 1000);

	if(RecordsTaskResult.HasSucceeded)
	{
		// RecordsTaskResultLoaded = True;
		// log("Records Loaded!");
		// LayerCustomEvent(Layer_RecordCenter, "PluginRecordsLoaded", [""]);
		declare Boolean RecordCenterVisible  for LocalUser;
		RecordCenterVisible = True;
		GetRecordsResume(SelectedFilter, FromOutside);
		RecordsTaskResult = Null;
	}
	else
	{
		log("["^Now^"] RecordsTaskResult has not succeeded");
		declare Boolean RecordCenterVisible  for LocalUser;
		RecordCenterVisible = False;
		PopUpManager::AddPopUp2(0, _("The connection to the Trackmania servers has been lost. Online features will become available once the connection has been re-established"), "", False);
		//log(Now ^ "> An unexpected error occured. Please refresh this page later.");
		RecordsTaskResult = Null;
	}
}

Void LoadRecordsTaskResult(Text SelectedFilter, Text BuddyLogin, Text SelectedGroup, Text FromOutside, Text BuddyName)
{
	declare CUILayer Layer_RecordCenterDetails for This;

	declare CWebServicesTaskResult 																		RecordsTaskResult				for Layer_RecordCenterDetails.LocalPage;
	// declare Boolean 																									RecordsTaskResultLoaded for Layer_RecordCenterDetails.LocalPage;

	// RecordsTaskResult = ManiaPlanet.MasterServer_GetBuddyChallengeRecordsComparison( DataMgr.MenuUserId, BuddyLogin );
	//CB: on release d'abord la tche prcdente, sinon a stack
	if(RecordsTaskResult != Null) {
		ManiaPlanet.MasterServer_ReleaseMSTaskResult(RecordsTaskResult.Id);
		log("RELEASE TASK BUDDY COMPARISON");
	}
	
	
	RecordsTaskResult = ScoreMgr.Campaign_GetBuddyMapRecordsComparison( DataMgr.MenuUserId, BuddyLogin );

	declare Time = Now;
	wait(!RecordsTaskResult.IsProcessing || Now >= Time + 5 * 1000);

	if(RecordsTaskResult.HasSucceeded)
	{
		// RecordsTaskResultLoaded = True;
		GetRecords(SelectedFilter, BuddyLogin, SelectedGroup, FromOutside, BuddyName);
		// log("Buddy Records Loaded!");
	}
	else
	{
		log("["^Now^"] BuddyChallenge RecordsTaskResult has not succeeded");
		log(Now ^ "> An unexpected error occured. Please refresh this page later.");
		PopUpManager::AddPopUp2(0, _("The connection to the Trackmania servers has been lost. Online features will become available once the connection has been re-established"), "", False);
	}
}

Void Loop() {
	foreach(Event in PendingEvents)
	{
		if(Event.Type == CMlAppEvent::EType::LayerCustomEvent)
		{
			//log("Received an event in RecordsHandler ! -> "^Event.CustomEventType);
			switch(Event.CustomEventType) {
				case "PopUpRequestError" : {
					PopUpManager::AddPopUp(0, 0, _("An unexpected error occured. Please refresh this page later."), "", False);
				}

				case "GetRecordsResume": {
					declare SelectedFilter 		= Event.CustomEventData[0];
					declare FromOutside 		= Event.CustomEventData[1];
					// GetRecordsResume(SelectedFilter, FromOutside);
					LoadRecordsTaskResult(SelectedFilter, FromOutside);
				}

				case "GetRecords": {
					declare SelectedFilter 		= Event.CustomEventData[0];
					declare BuddyLogin 			= Event.CustomEventData[1];
					declare SelectedGroup 		= Event.CustomEventData[2];
					declare FromOutside 		= Event.CustomEventData[3];
					declare BuddyName		 		= Event.CustomEventData[4];
					// GetRecords(SelectedFilter, BuddyLogin, SelectedGroup, FromOutside);
					LoadRecordsTaskResult(SelectedFilter, BuddyLogin, SelectedGroup, FromOutside, BuddyName);
				}

				case "RecordPlayTrack": {

					// 0 MAP PATH
					// 1 DIFFICULTY
					// 2 ENVIRONMENT
					// 3 GHOST URL

					declare Boolean RecordCenterVisible  for LocalUser;
					RecordCenterVisible = False;
					G_RecordCenterMode = True;
					ManiaPlanet.CustomMenu_Request("ChallengeOpponent", [Event.CustomEventData[0], Event.CustomEventData[1], Event.CustomEventData[2], Event.CustomEventData[3]]);
					declare CUILayer Layer_RecordCenterDetails for This;
					Layer_RecordCenterDetails.IsVisible = False;
				}

				case "ReleaseRecordsTaskResult":
				{
					ReleaseRecordsTaskResult(Event.CustomEventData[0]);
				}
			}
		}
	}
}
