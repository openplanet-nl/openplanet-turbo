#Include "TextLib"			as TextLib
#Include "Libs/Nadeo/TMConsole/Logger.Script.txt"											as Logger
#Include "Libs/Nadeo/TMConsole/Menu/MenuLayers.Script.txt"									as Layers
#Include "Libs/Nadeo/TMConsole/PopUpManager.Script.txt"										as PopUpManager
#Include "Libs/Nadeo/TMConsole/Menu/Controller/ServerController.Script.txt" 				as ServerController
#Include "Libs/Nadeo/TMConsole/Menu/Controller/BuddiesOnlinePresenceController.Script.txt" 	as OnlinePresenceController
#Include "Libs/Nadeo/TMConsole/Menu/Controller/OnlinePrivilegeController.Script.txt" 		as OnlinePrivilegeController
#Include "Libs/Nadeo/TMConsole/Menu/LoadingScreen.Script.txt"								as LoadingScreen

declare Boolean G_PrevIsLimitedToCanyon;

Void WarnNotEnoughParams(CMlAppEvent _Event) {
	Logger::Warn("[Menucontroller] event "^_Event.CustomEventType^": not enough params ["^_Event.CustomEventData^"]");
}

Void RequestServerStatus(Integer _ServerId)
{
	ServerController::GetServerOnlineStatus("JoinAFriend", _ServerId);
}

Void RequestServerStart(Integer _RoomIndex, Text _Password) {
	ServerController::ServerStart("JoinAFriend", _RoomIndex, _Password);
}

Void RequestServerStop(Integer _RoomIndex) {
	ServerController::ServerStop("JoinAFriend", _RoomIndex);
}

Void RequestServerMaps(Integer _RoomIndex) {
	ServerController::GetServerMaps("JoinAFriend", _RoomIndex);
}

Void RequestBuddiesServerInfo(Integer _Offset, Integer _Length, Text _BuddiesInformation, Boolean _CanyonOnly)
{
	ServerController::GetBuddiesServerInfo("JoinAFriend", _Offset, _Length, _BuddiesInformation, _CanyonOnly);
}

Void RequestBuddiesOnlinePresence() {
	OnlinePresenceController::Layer_GetBuddiesOnlinePresence("JoinAFriend");
}

Void CheckTargeted_ViewUgcRequired(Text[] _Logins) {
	OnlinePrivilegeController::CheckTargeted_ViewUgcRequired("JoinAFriend", _Logins);
}

Void Loop() {
	declare Boolean IsLimitedToCanyon = ManiaPlanet.TmTurbo_IsPartialInstall || ManiaPlanet.TmTurbo_IsDemo;
	if (G_PrevIsLimitedToCanyon != IsLimitedToCanyon)
	{
		declare CUILayer JoinAFriend = Layers::Get("JoinAFriend");
		LayerCustomEvent(JoinAFriend, "SetIsLimitedToCanyon", [TextLib::ToText(IsLimitedToCanyon)]);
	}
	
	foreach(Event in PendingEvents) {
		if(Event.Type != CMlAppEvent::EType::LayerCustomEvent) continue;
		switch(Event.CustomEventType) {
			case "RequestBuddiesOnlinePresence" : {
				RequestBuddiesOnlinePresence();
				log("RequestOnlinePresenceHandler");
			}
			case "RequestBuddiesServerInfo" : {
				if (Event.CustomEventData.count < 4)
					WarnNotEnoughParams(Event);
				else {
					declare Integer Offset = TextLib::ToInteger(Event.CustomEventData[0]);
					declare Integer Length = TextLib::ToInteger(Event.CustomEventData[1]);
					RequestBuddiesServerInfo(Offset, Length, Event.CustomEventData[2], Event.CustomEventData[3] == "True");
				}
			}
			case "RequestServerStatus" : {
				if (Event.CustomEventData.count < 1)
					WarnNotEnoughParams(Event);
				else
					RequestServerStatus(TextLib::ToInteger(Event.CustomEventData[0]));
			}
			case "RequestServerStart" : {
				if (Event.CustomEventData.count < 2)
					WarnNotEnoughParams(Event);
				else
					RequestServerStart(TextLib::ToInteger(Event.CustomEventData[0]), Event.CustomEventData[1]);
			}
			case "RequestServerStop" : {
				if (Event.CustomEventData.count < 1)
					WarnNotEnoughParams(Event);
				else
					RequestServerStop(TextLib::ToInteger(Event.CustomEventData[0]));
			}
			case "RequestServerMaps" : {
				if (Event.CustomEventData.count < 1)
					WarnNotEnoughParams(Event);
				else
					RequestServerMaps(TextLib::ToInteger(Event.CustomEventData[0]));
			}
			case "PopUpLevelTooLow" : {
				if (Event.CustomEventData.count < 2)
					WarnNotEnoughParams(Event);
				else
					PopUpManager::AddPopUp2(10 , TextLib::Compose(_("You can't join this room. You need at least %1 Ladder Points to enter, but you have only %2 of them."), ""^Event.CustomEventData[1], ""^Event.CustomEventData[0]), "Ok", False);
			}
			case "PopUpLevelTooHigh" : { //oblsolÃ¨te non ?
				if (Event.CustomEventData.count < 2)
					WarnNotEnoughParams(Event);
				else
					PopUpManager::AddPopUp2(10 , TextLib::Compose(_("You can't join this room. The upper limit to enter is %1 Ladder Points, but you already have %2 of them."), ""^Event.CustomEventData[1], ""^Event.CustomEventData[0]), "Ok", False);
			}
			case "PopUp_CanyonRestricted" : {
				if (ManiaPlanet.TmTurbo_IsPartialInstall)
					PopUpManager::AddPopUp2(0, _("You will only see Canyon rooms because the game is not fully installed."), "", False);
				else if (ManiaPlanet.TmTurbo_IsDemo)
					PopUpManager::AddPopUp2(0, TextLib::GetTranslatedText(_("Welcome to the free one-hour online trial. "))^TextLib::GetTranslatedText(_("You will only see Canyon rooms because you have the Trial version. Unlock the full game to unlock all environments")), "", False);
			}
			case "JoinServer" : {
				if (Event.CustomEventData.count < 1)
					WarnNotEnoughParams(Event);
				else
					ManiaPlanet.OpenLink(Event.CustomEventData[0], CManiaPlanet::ELinkType::ManialinkBrowser);
			}
			case "ServerInfo_CheckTargeted_ViewUgcRequired" : {
				CheckTargeted_ViewUgcRequired(TextLib::Split(",", Event.CustomEventData[0]));
			}
			case "ServerInfo_Check_PlayMultiplayerSession" : {
				OnlinePrivilegeController::Check_PlayMultiplayerSession("JoinAFriend");
				LoadingScreen::SetLoadingType(2, 0);
			}
			case "SetLiveStreamingEnabled" : {
				ManiaPlanet.IsLiveStreamingEnabled = (Event.CustomEventData[0] == "True");
			}
			case "GetIsLimitedToCanyon" : {
				declare Boolean IsLimitedToCanyon = ManiaPlanet.TmTurbo_IsPartialInstall || ManiaPlanet.TmTurbo_IsDemo;
				declare CUILayer JoinAFriend = Layers::Get("JoinAFriend");
				LayerCustomEvent(JoinAFriend, "SetIsLimitedToCanyon", [TextLib::ToText(IsLimitedToCanyon)]);
			}
		}
	}
}