#RequireContext CPlugin

Text CreateManialink(Boolean HelpDisplay)
{
	declare MLText =
	"""
		<script><!--
			Void OnChangeHelpDisplay()	{ declare Boolean ChangeHelpDisplay for Page; ChangeHelpDisplay = True; }
			
			main () {
				while(True) {
					yield;
					foreach(Event in PendingEvents) {
						if(Event.Type == CMlEvent::Type::MouseClick)
						{
								if(Event.ControlId == "PluginQuad" || Event.ControlId == "HelpOkQuad" || Event.ControlId == "HelpOkLabel")
									OnChangeHelpDisplay();
						}
					}
				}
			}
		--></script>
		<frame>
			<quad id="PluginQuad" posn="150 5" z-index="1" sizen="10 10" halign="center" valign="center" style="UIConstructionSimple_Buttons" substyle="Underground" ScriptEvents="1"/>
			
	""";
	if(HelpDisplay)
	{
		MLText ^=
		"""
			<quad id="HelpTitleQuad" posn="0 50" z-index="-1" sizen="80 15" halign="center" valign="center" style="Bgs1InRace" substyle="BgTitle3_1"/>
			<label id="HelpTitleLabel" text="AutoUnderground Plugin" posn="0 50" halign="center" valign="center" style="TextTitle3"/>
			
			<quad id="HelpQuad" posn="0 43" z-index="-2" sizen="100 30" halign="center" valign="top" style="Bgs1" substyle="BgList"/>
				
				<label id="HelpText" text="Automatically switch to Underground ViewMode or Normal ViewMode according to selected block" posn="0 35" sizen="90 3" maxlines="3" autonewline="1" halign="center" valign="center" textsize="2"/>
				<quad id="HelpOkQuad" posn="0 20" z-index="-1" sizen="30 6" halign="center" valign="center" style="Bgs1" substyle="BgCard" ScriptEvents="1"/>
				<label id="HelpOkLabel" text="OK" posn="0 20" halign="center" valign="center" style="TextButtonSmall" textsize="2"/>
			
		""";
	}
	MLText ^=
	"""
		</frame>
	""";
	return MLText;
}

/////////////////////////////////////
// Main
/////////////////////////////////////

main()
{
	log("AutoUnderground.Script starts");
	
	LayersDefaultManialinkVersion = 3;
	
	declare LatestBlockModelId = NullId;
	declare HelpDisplay = False;
	declare ChangeHelpDisplay for ManialinkPage = False;
	
	ManialinkText = CreateManialink(HelpDisplay);
	
	while(True)
	{
		yield;
		// change help display if necessary
		if(ChangeHelpDisplay)
		{
			ChangeHelpDisplay = False;
			HelpDisplay = !HelpDisplay;
			ManialinkText = CreateManialink(HelpDisplay);
		}
		
		// change ViewMode if necessary
		if(CursorBlockModel != Null)
		{
			if(CursorBlockModel.Id != LatestBlockModelId)
			{
				LatestBlockModelId = CursorBlockModel.Id;
				if(CursorBlockModel.VariantAir!=Null && CursorBlockModel.VariantAir.IsAllUnderground)
					UndergroundMode = True;
				else
					UndergroundMode = False;
			}
		}
	}
}