
#Include "Libs/Openplanet/Manialink2.Script.txt"								as Manialink
#Include "Libs/Openplanet/TMConsole/Menu/LibLoading.Script.txt"				as Loading
#Include "Libs/Openplanet/TMConsole/PanelParameter.Script.txt"	            as PanelParameter
#Include "Libs/Openplanet/TMConsole/Live/LiveHelpers.Script.txt"				as LiveHelpers
#Include "Libs/Openplanet/TMConsole/Menu/LibOnlineGameplay.Script.txt"		as GamePlay
#Include "Libs/Openplanet/UIPadButtons.Script.txt"							as LibUIButtons

#Include "Libs/Openplanet/TMConsole/LayerBuilders/LBBuddiesChallengeBrowser_Browser.Script.txt" 		as BuddiesBrowser
#Include "Libs/Openplanet/TMConsole/LayerBuilders/LBBuddiesChallengeBrowser_Options.Script.txt" 		as BuddiesChallengeOptions

#Const C_ImgBase 	"file://Media/Manialinks/Nadeo/TMConsole/Images/"

#Const Count_InstancesY				11

Text Build(Text _ShowAnim, Text _HideAnim) {

	//IMAGES
	declare Text IMGBoard       	= C_ImgBase^"board_Empty.tga";	
	declare Text IMGEchelonPrefix  	= C_ImgBase^"Online/BrowseServers/echelons/Echelon_";
	
	//SIZES
	declare Real	SM_SizeTotalX            = 259.0;
	declare Real	SM_SizeTotalY            = 150.;
	declare Real	SM_SizeY                 = 8.27;

	declare Integer Platform = LibUIButtons::GetPlatform();
	
	//POSITIONS
	declare Real	PositionFirstQuadX       = 0.;
	declare Real	PositionFirstQuadY       = 36.3;

	//L16N Displayed while the browser is loading
	Loading::Init(<SM_SizeTotalX, SM_SizeY*Count_InstancesY>, <PositionFirstQuadX, (PositionFirstQuadY - (SM_SizeY*(Count_InstancesY+1)) / 2.)>, 400, _("Retrieving information. Please wait."));

	declare MLText = """	
		<frame id="Frame_General">
			<quad id="Quad_Board" posn="0 0 0" halign="center" valign="center" sizen="{{{259.5}}} {{{SM_SizeTotalY}}}" image="{{{IMGBoard}}}" />
	""";

	/////////////////////////
	// LOADING
	/////////////////////////		
	MLText ^= """{{{Loading::InjectInManialink()}}}""";
	
	/////////////////////////
	// BROWSER
	/////////////////////////
	MLText ^= """{{{BuddiesBrowser::InjectInManialink()}}}""";
	
	/////////////////////////
	// CHALLENGEOPTIONS
	/////////////////////////
	MLText ^= """{{{BuddiesChallengeOptions::InjectInManialink()}}}""";
	
	MLText ^= """
	</frame>
	
	<script><!--
		#Const DebugName  "L_BuddiesChallengeBrowser"
		#Const C_IsServerOnlineRequestDelay		2000 //on attend 2s entre chaque requete
		#Const C_TimeTransitionPage2  {{{PanelParameter::GetTimeTransitionPage2()}}}
		
		#Const C_STATUS_NONE 				0
		#Const C_STATUS_BROWSER				1
		#Const C_STATUS_CHALLENGEOPTIONS	2
		
		#Const C_POSTINSTALLACTION_NONE		0
		#Const C_POSTINSTALLACTION_TRAIN	1
		#Const C_POSTINSTALLACTION_RACE		2
		#Const C_POSTINSTALLACTION_BUDDIES	3
		
		
		{{{Manialink::GetIncludes()}}}
		{{{Manialink::Load()}}}
		
		declare Integer						G_LockInput;
		declare Integer						G_HideTime;
		declare Integer 					G_ChallengeBrowserStatus;
		
		declare Boolean						G_IsLimitedToCanyon;
		
		{{{BuddiesBrowser::InjectInGlobals()}}}
		{{{BuddiesChallengeOptions::InjectInGlobals()}}}
		
		declare Boolean						G_IsInChallengeTracks;
		declare Boolean						G_IsInChallengeManager;
		declare Integer						G_PostInstallAction;
		
		//Images
		declare Text[] G_Images_Echelon;
		
		//Gameplay
		declare Text[]						G_ChallengesStyles;
		declare Text[]						G_ChallengesDurations;
		
		{{{Loading::InjectInGlobals()}}}
		{{{Loading::InjectInFunctions()}}}
		
		{{{LiveHelpers::InjectConverters()}}}
		{{{LiveHelpers::InjectFilters()}}}
		
		Void HidePage()
		{
			G_HideTime  = Now + C_TimeTransitionPage2;
			G_LockInput = Now + C_TimeTransitionPage2;
		}
			
		Void InstallChallenge()
		{
			declare Text[Text][] RequestedChallenges for Page;
			SendCustomEvent("RequestInstallChallenge", [RequestedChallenges[G_CurrentSelectedChallengeIndex]["id"]]);
		}
		
		Void ChallengeBuddies(Text _ChallengeId)
		{
			SendCustomEvent("SetMenuContext", ["ChallengeBuddies", _ChallengeId]);
			SendCustomEvent("GotoPage", ["ChallengeBuddies"]);
			{{{_HideAnim}}}
			SendCustomEvent("DetachPage", ["BuddiesChallengeBrowser"]);
			G_IsInChallengeManager = True;
		}
		
		{{{LibUIButtons::Inject_SetInputInfoTextSelected()}}}
	
		Void ToggleInputInfo(Text _Context, Text _Input, Boolean _Hovered) {
			declare CMlFrame FrameInput <=> (Page.GetFirstChild(_Context^"_InputInfo_"^_Input) as CMlFrame);
			declare CMlLabel LabelInputInfo <=> (FrameInput.GetFirstChild("Label_Name") as CMlLabel);
			SetInputInfoTextSelected(LabelInputInfo, _Hovered);
		}
			
		{{{BuddiesBrowser::InjectInFunctions()}}}
		{{{BuddiesChallengeOptions::InjectInFunctions()}}}
		
		//////////////////
		// STATUS SWITCH
		//////////////////
		Void ChallengeBrowserStatus_Switch(Integer _NewStatus)
		{
			switch (G_ChallengeBrowserStatus)
			{
				case C_STATUS_BROWSER 			: Null;
				case C_STATUS_CHALLENGEOPTIONS	: ChallengeOptions_Hide();
			}
			
			G_ChallengeBrowserStatus = _NewStatus;
			
			switch (G_ChallengeBrowserStatus)
			{
				case C_STATUS_BROWSER 				: Browser_Show();
				case C_STATUS_CHALLENGEOPTIONS		: ChallengeOptions_Show();
			}
		}
		
		Void Open()
		{
			ChallengeBrowserStatus_Switch(C_STATUS_BROWSER);
		}
		
		Void Quit()
		{
			{{{_HideAnim}}}
			SendCustomEvent("GotoPrevious", [""]);
			SendCustomEvent("MainSelector_SelectBuddiesChallengesBrowser", [""]);
		}
		
		//////////////////
		// INIT
		//////////////////
		Void Init()
		{
			G_IsLimitedToCanyon 		= False;
			G_IsInChallengeTracks 		= False;
			G_IsInChallengeManager 		= False;
			G_ChallengeBrowserStatus = C_STATUS_NONE;
			
			{{{Loading::InjectInInit()}}}
			
			{{{BuddiesChallengeOptions::InjectInInit()}}}
			{{{BuddiesBrowser::InjectInInit()}}}
			
			G_ChallengesStyles = {{{dump(GamePlay::GetRoomStyles())}}};
			G_ChallengesDurations = {{{dump(GamePlay::GetDurations())}}};
			
			G_Images_Echelon = ["{{{IMGEchelonPrefix}}}0.tga", 
								"{{{IMGEchelonPrefix}}}1.tga", 
								"{{{IMGEchelonPrefix}}}2.tga",
								"{{{IMGEchelonPrefix}}}3.tga", 
								"{{{IMGEchelonPrefix}}}4.tga", 
								"{{{IMGEchelonPrefix}}}5.tga", 
								"{{{IMGEchelonPrefix}}}6.tga", 
								"{{{IMGEchelonPrefix}}}7.tga",	
								"{{{IMGEchelonPrefix}}}8.tga", 
								"{{{IMGEchelonPrefix}}}9.tga"];	
		}
		
		Void OnRequestedChallengesReceived()
		{
			declare Text[Text][] RequestedChallenges for Page;
			
			G_RequestError = False;
			Loading_SetIsLoading(False);
			
			declare Real NbPages = (RequestedChallenges.count*1.0 - 1)/({{{Count_InstancesY}}}*1.0);
			G_Prev_PageIndex = G_PageIndex;
			SetPageMax(ML::CeilingInteger(NbPages));
			if (G_NextPageOnRequestCompletion && G_Prev_PageIndex < NbPages - 1)
				G_Prev_PageIndex += 1;
			G_PageIndex = ML::Min(G_Prev_PageIndex, ML::CeilingInteger(NbPages)-1);
			if (G_PageIndex < 0)
				G_PageIndex = 0;
			
			Update2DArray(True);
			UpdateInputsInfo();
		}
		
		Void OnRequestedChallengesFailure()
		{
			declare Text[Text][] RequestedChallenges for Page;
			
			G_RequestError = True;
			Loading_SetIsLoading(False);
			declare Real NbPages = (RequestedChallenges.count*1.0 - 1)/({{{Count_InstancesY}}}*1.0);
			SetPageMax(ML::CeilingInteger(NbPages));
			Update2DArray(True);
			UpdateInputsInfo();
		}
		
		//////////////////
		// MAIN
		//////////////////
		main() {
			Init();
			
			EnableMenuNavigation(True,False,Null,2);

			declare Text[Text][] RequestedChallenges for Page;
			declare Text[Text][] RequestedChallengeMaps for Page;
			declare Integer[]	 InstalledChallengeIds for Page;
		
			G_HideTime = -1;
			
			declare Integer ActiveButtonPlatform = {{{Platform}}};

			{{{BuddiesBrowser::InjectBeforeLoop()}}}
			{{{BuddiesChallengeOptions::InjectBeforeLoop()}}}
			
			while(True) {
				yield;
				
				if(! PageIsVisible) continue;
				LibManialink_AnimLoop();
				
				{{{BuddiesBrowser::InjectInLoop()}}}
				{{{BuddiesChallengeOptions::InjectInLoop()}}}
				
				if(G_HideTime <= Now && G_HideTime != -1)
				{
					G_HideTime = -1;
					SendCustomEvent("DetachPage", ["BuddiesChallengeBrowser"]);
				}

				{{{Loading::InjectInLoop()}}}	
				
				if (Input.PendingEvents.count != 0) {
					declare Integer LastInputPlatform;
					switch (Input.PendingEvents[Input.PendingEvents.count-1].Pad.Type) {
						case CInputPad::EPadType::Keyboard 		: LastInputPlatform = {{{LibUIButtons::GetPlatform("PC")}}};
						case CInputPad::EPadType::PlayStation 	: LastInputPlatform = {{{LibUIButtons::GetPlatform("PS4")}}};
						case CInputPad::EPadType::XBox 			: LastInputPlatform = {{{LibUIButtons::GetPlatform("XB1")}}};
						default 								: LastInputPlatform = {{{LibUIButtons::GetPlatform("PC")}}};
					}
					if (LastInputPlatform != ActiveButtonPlatform)
						SendCustomEvent("SetActiveButtonPlaform", ["BuddiesChallengeBrowser", TL::ToText(LastInputPlatform)]);
				}
				
				//EVENTS
				foreach(Event in PendingEvents) {
					switch(Event.Type) {
						case CMlEvent::Type::PluginCustomEvent: {
							switch(Event.PluginCustomEventType) {
								case "Show" : {
									{{{_ShowAnim}}}
									ToggleInputInfo("Browser", "Select", False);
									ToggleInputInfo("Browser", "Cancel", False);
									ToggleInputInfo("Browser", "LStickPress", False);
									ToggleInputInfo("ChallengeOptions", "Select", False);
									ToggleInputInfo("ChallengeOptions", "Cancel", False);
									EnableMenuNavigation(True,False,Null,2);
									
									if (G_IsInChallengeTracks)
									{
										G_IsInChallengeTracks = False;
										ChallengeOptions_Refresh();
									} else if (G_IsInChallengeManager) {
										G_IsInChallengeManager = False;
										ChallengeOptions_Refresh();
									} else {
										SendCustomEvent("BuddiesChallengesBrowsed", Text[]);
										ChallengeBrowserStatus_Switch(C_STATUS_BROWSER);
										G_IndexSelectY = 0;
									}
									G_LastRefreshTime = Now;
									SendCustomEvent("GetIsLimitedToCanyon", Text[]);
									SendCustomEvent("GetActiveButtonPlatform", ["BuddiesChallengeBrowser"]);
								}
								case "SetActiveButtonPlatform" : {
									ActiveButtonPlatform = TL::ToInteger(Event.CustomEventData[0]);
									Browser_UpdateHelpersForPlatform(ActiveButtonPlatform);
									ChallengeOptions_UpdateHelpersForPlatform(ActiveButtonPlatform);
								}
								case "Hide" : {
									{{{_HideAnim}}}
									HidePage();
								}
								case "Request_GetBuddiesChallenges_Success" : {
									OnRequestedChallengesReceived();
								}
								case "Request_GetBuddiesChallenges_Failure" : {
									OnRequestedChallengesFailure();
								}
								case "Request_GetBuddiesChallenge_Success" : {
									ChallengeOptions_OnChallengeReceived();
								}	
								case "Request_GetBuddiesChallenge_Failure" : {
									ChallengeBrowserStatus_Switch(C_STATUS_BROWSER);
								}
								case "Request_InstallChallenge_Success" : {
									declare Text ChallengeId = RequestedChallenges[G_CurrentSelectedChallengeIndex]["id"];
									switch (G_PostInstallAction)
									{
										case C_POSTINSTALLACTION_TRAIN 		: if (!G_HasARace && G_CanTrain) ChallengeOptions_OnTrain(ChallengeId);
										case C_POSTINSTALLACTION_RACE 		: ChallengeOptions_OnRace(ChallengeId);
										case C_POSTINSTALLACTION_BUDDIES 	: ChallengeBuddies(ChallengeId);
										case C_POSTINSTALLACTION_NONE		:
										{
											SendCustomEvent("PopUpChallengeInstalled", Text[]);
											ChallengeOptions_Refresh();
										}
									}
								}
								case "Request_RemoveBuddyChallenge_Success" : {
									SendCustomEvent("PopUpChallengeRemoved", Text[]);
									Loading_SetIsLoading(False);
									ChallengeBrowserStatus_Switch(C_STATUS_BROWSER);
								}
								case "Request_RemoveBuddyChallenge_Failure" : {
									Loading_SetIsLoading(False);
								}
								case "GiveUpRace" : {
									if (Event.CustomEventData[0] == "True")
									{
										declare Text[Text] Challenge for Page;
										SendCustomEvent("GiveUpRace", [Challenge["id"], Challenge["currentrace_id"]]);
										G_MustGotoRaceOnRaceGivenUp = True;
									}
								}
								case "Request_FinishRace_Success" : {
									if (G_MustGotoRaceOnRaceGivenUp)
									{
										ChallengeOptions_GotoRace();
										G_MustGotoRaceOnRaceGivenUp = False;
									}
								}	
								case "Request_FinishRace_Failure" : {
									if (G_MustGotoRaceOnRaceGivenUp)
										G_MustGotoRaceOnRaceGivenUp = False;
								}
								case "SetIsLimitedToCanyon" : {
									declare Boolean IsLimitedToCanyon = Event.CustomEventData[0] == "True";
									if (IsLimitedToCanyon != G_IsLimitedToCanyon)
									{
										G_IsLimitedToCanyon = IsLimitedToCanyon;
										if (G_IsLimitedToCanyon)
											SendCustomEvent("PopUp_CanyonRestricted", Text[]);
										if (G_IsLimitedToCanyon || G_ChallengeBrowserStatus == C_STATUS_BROWSER)
											ChallengeBrowserStatus_Switch(C_STATUS_BROWSER);
									}
								}
							}
						}
						
						case CMlEvent::Type::KeyPress: {
							if (G_ChallengeBrowserStatus == C_STATUS_BROWSER) {
								{{{BuddiesBrowser::InjectInKeyPress()}}}
							}
						}
						case CMlEvent::Type::MouseClick: {
							if (ActiveButtonPlatform !=  {{{LibUIButtons::GetPlatform("PC")}}})
								SendCustomEvent("SetActiveButtonPlaform", ["BuddiesChallengeBrowser", TL::ToText({{{LibUIButtons::GetPlatform("PC")}}})]);
							switch (G_ChallengeBrowserStatus)
							{
								case C_STATUS_BROWSER :
								{
									{{{BuddiesBrowser::InjectInMouseClick()}}}
								}
								case C_STATUS_CHALLENGEOPTIONS :
								{
									{{{BuddiesChallengeOptions::InjectInMouseClick()}}}
								}
							}
						}
						case CMlEvent::Type::MouseOver: {
							switch (G_ChallengeBrowserStatus)
							{
								case C_STATUS_BROWSER :
								{
									{{{BuddiesBrowser::InjectInMouseOver()}}}
								}
								case C_STATUS_CHALLENGEOPTIONS :
								{
									{{{BuddiesChallengeOptions::InjectInMouseOver()}}}
								}
							}
						}
						case CMlEvent::Type::MouseOut: {
							switch (G_ChallengeBrowserStatus)
							{
								case C_STATUS_BROWSER :
								{
									{{{BuddiesBrowser::InjectInMouseOut()}}}
								}
								case C_STATUS_CHALLENGEOPTIONS :
								{
									{{{BuddiesChallengeOptions::InjectInMouseOut()}}}
								}
							}
						}
						case CMlEvent::Type::MenuNavigation: {
							if(G_LockInput + 50 > Now) continue;
							G_LockInput = Now;
							switch (G_ChallengeBrowserStatus)
							{
								case C_STATUS_BROWSER :
								{
									{{{BuddiesBrowser::InjectInMenuNavigation()}}}
								}
								case C_STATUS_CHALLENGEOPTIONS :
								{
									{{{BuddiesChallengeOptions::InjectInMenuNavigation()}}}
								}
							}
						}
					}
				}
			}
		}
	--></script>
	""";
	
	return MLText;
}
