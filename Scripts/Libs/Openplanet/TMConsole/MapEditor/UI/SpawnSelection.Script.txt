/** 
 * Spawn selection UI
 */

#Const Version		"2016-03-02"
#Const ScriptName	"SpawnSelection.Script.txt"

#Include "TextLib" as TL
#Include "Libs/Openplanet/Buttons.Script.txt" as Buttons
#Include "Libs/Openplanet/Manialink3.Script.txt" as Manialink
#Include "Libs/Openplanet/TMConsole/MapEditor/Layers.Script.txt" as Layers
#Include "Libs/Openplanet/TMConsole/MapEditor/Sound.Script.txt" as Sound
#Include "Libs/Openplanet/TMConsole/MapEditor/UI/Colors.Script.txt" as Colors
#Include "Libs/Openplanet/TMConsole/MapEditor/UI/Stylesheet.Script.txt" as Styles

// ---------------------------------- //
// Constants
// ---------------------------------- //
#Const C_SpawnsNb		12
/// Events
#Const C_Event_SpawnHover	"LibSpawnSelection_SpawnHover"
#Const C_Event_SpawnClick	"LibSpawnSelection_SpawnClick"
#Const C_Event_SpawnNext	"LibSpawnSelection_SpawnNext"
#Const C_Event_SpawnPrev	"LibSpawnSelection_SpawnPrev"

// ---------------------------------- //
// Globales
// ---------------------------------- //
declare Integer[] G_Spawns;			///< List of spawns
declare Integer G_SpawnsStart;		///< Starting index for the UI list
declare Integer G_SpawnUpdate;		///< Last spawn update time
declare Integer G_Spawn;			///< Selected spawn

// ---------------------------------- //
// Functions
// ---------------------------------- //
// ---------------------------------- //
// Private
// ---------------------------------- //
// ---------------------------------- //
/** Get the spawn selection manialink
 *
 *	@return							The manialink
 */
Text Private_GetSpawnSelectionML() {
	declare Spawns = "";
	declare SpawnsSizeX = Styles::GetHeaderSize2();
	declare SpawnSize = <SpawnsSizeX / C_SpawnsNb, 4.>;
	declare ButtonSize = 2.;
	declare FullHeaderSizeX = SpawnsSizeX + (2 * ButtonSize);
	for (I, 0, C_SpawnsNb+1) {
		declare PosX = (I-1) * Styles::GetSize2(SpawnSize, Styles::CircleSize_Bottom()).X;
		Spawns ^= """<frameinstance pos="{{{PosX}}} 0" modelid="Framemodel_Spawn" />""";
	}
	
	declare NextSpawn = Buttons::Editor_NextSpawn();
	declare PrevSpawn = Buttons::Editor_PrevSpawn();
	
	return """
<manialink version="3" name="MapEditor:Spawn-selection">
<stylesheet>
	{{{Styles::Get()}}}
</stylesheet>
<framemodel id="Framemodel_Spawn">
	{{{Styles::BuildQuad(<0., 0., 0.>, <SpawnSize.X, SpawnSize.Y>, Styles::CircleSize_Bottom(), Styles::Align_LeftTop(), ["bgcolor" => Colors::GetColorML(Colors::Color_Black()), "scriptevents" => "1", "id" => "Quad_Select"])}}}
	{{{Styles::BuildQuad(<SpawnSize.X*0.5, -SpawnSize.Y*0.5, 1.>, <SpawnSize.X-0.5, SpawnSize.Y-0.5>, Styles::CircleSize_Bottom(), Styles::Align_CenterCenter(), ["id" => "Quad_Spawn"])}}}
	{{{Styles::BuildLabel(<SpawnSize.X, -SpawnSize.Y, 2.>, <SpawnSize.X, SpawnSize.Y>, Styles::CircleSize_Bottom(), Styles::Align_RightBottom(), ["class" => "turbo2-text-legend", "id" => "Label_Spawn"])}}}
</framemodel>
<frame z-index="0" id="Frame_Global">
	<frame pos="0 -92" hidden="1" id="Frame_SpawnSelection">
		<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<-FullHeaderSizeX*0.5, 0., 0.>, Styles::CircleSize_Bottom()))}}}>
			{{{Styles::BuildBackground(<0., 0., 0.>, <FullHeaderSizeX, SpawnSize.Y+1.>, Styles::CircleSize_Bottom(), Styles::Align_LeftTop())}}}
			{{{Styles::BuildLine(<0., 0., 1.>, <FullHeaderSizeX, 1.>, Styles::CircleSize_Bottom(), Styles::Align_LeftTop(), Colors::Color_Purple(), True, True, Text[Text])}}}
			{{{Styles::BuildLabel(<FullHeaderSizeX*0.5, -0.45, 2.>, <FullHeaderSizeX, 1.>, Styles::CircleSize_Bottom(), Styles::Align_CenterCenter2(), [
				"text" => 
				//L16N [MapEditor] Select the location where to start the test race
				_("Select where you want to start"), "class" => "turbo2-text-title"
			])}}}
			<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<ButtonSize, -1., 3.>, Styles::CircleSize_Bottom()))}}} {{{Styles::BuildSize2(Styles::GetSize2(<SpawnsSizeX, SpawnSize.Y>, Styles::CircleSize_Bottom()))}}}>
				<frame z-index="0" id="Frame_Spawns">
					{{{Spawns}}}
				</frame>
			</frame>
			<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<0., -1., 4.>, Styles::CircleSize_Bottom()))}}}>
				{{{Styles::BuildQuad(<0., 0., 0.>, <ButtonSize, SpawnSize.Y>, Styles::CircleSize_Bottom(), Styles::Align_LeftTop(), ["bgcolor" => Colors::GetColorML(Colors::Color_Black())])}}}
				{{{Styles::BuildButton(PrevSpawn, <ButtonSize*0.5, -SpawnSize.Y*0.5, 1.>, <ButtonSize-0.5, ButtonSize-0.5>, Styles::CircleSize_Bottom(), Styles::Align_CenterCenter(), ["scriptevents" => "1", "id" => "Button_PrevSpawn"])}}}
			</frame>
			<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<ButtonSize+SpawnsSizeX, -1., 5.>, Styles::CircleSize_Bottom()))}}}>
				{{{Styles::BuildQuad(<0., 0., 0.>, <ButtonSize, SpawnSize.Y>, Styles::CircleSize_Bottom(), Styles::Align_LeftTop(), ["bgcolor" => Colors::GetColorML(Colors::Color_Black())])}}}
				{{{Styles::BuildButton(NextSpawn, <ButtonSize*0.5, -SpawnSize.Y*0.5, 1.>, <ButtonSize-0.5, ButtonSize-0.5>, Styles::CircleSize_Bottom(), Styles::Align_CenterCenter(), ["scriptevents" => "1", "id" => "Button_NextSpawn"])}}}
			</frame>
			<frame {{{Styles::BuildPosition2(Styles::GetPosition2(<ButtonSize, -1., 10.>, Styles::CircleSize_Bottom()))}}}>
				<frame id="Frame_Selector">
					{{{Styles::BuildQuad(<SpawnSize.X*0.5, -SpawnSize.Y*0.51, 1.>, <SpawnSize.X+2.1, SpawnSize.Y+1.5>, Styles::CircleSize_Bottom(), Styles::Align_CenterCenter(), ["class" => "turbo2-selector"])}}}
				</frame>
			</frame>
		</frame>
	</frame>
</frame>
<script><!--
#Include "TextLib" as TL
{{{Manialink::DefaultIncludes()}}}

#Const C_ClickTick	100
#Const C_InitTick	500

{{{Manialink::Animations(["EaseOutQuad"])}}}
{{{Buttons::ML_Functions()}}}

Void SetVisibility(Boolean _Visible) {
	if (_Visible) {
		declare L_PositionY for Page = 0.;
		LibManialink_Anim("<frame pos=\"0 "^L_PositionY^"\" hidden=\"0\" id=\"Frame_SpawnSelection\" />", 250, "EaseOutQuad");
	} else {
		LibManialink_Anim({{{Manialink::Inject("""<frame pos="0 -92" hidden="1" id="Frame_SpawnSelection" />""")}}}, 250, "EaseOutQuad");
	}
}

Void SetSpawns(Integer[] _Spawns, Integer _SpawnKey, Integer _Shift) {
	declare L_SpawnsNames for Page = Text[Integer];
	declare Frame_Spawns <=> (Page.GetFirstChild("Frame_Spawns") as CMlFrame);
	
	foreach (Key => Control in Frame_Spawns.Controls) {
		declare Frame_Spawn <=> (Control as CMlFrame);
		
		if (_Spawns.existskey(Key)) {
			declare BlockKey = _Spawns[Key];
			
			declare Quad_Select <=> (Frame_Spawn.GetFirstChild("Quad_Select") as CMlQuad);
			declare SpawnSelectKey for Quad_Select = -1;
			SpawnSelectKey = BlockKey;
			
			declare CBlockModel BlockModel;
			if (Editor.Blocks.existskey(BlockKey)) {
				BlockModel <=> Editor.Blocks[BlockKey].BlockModel;
			}
			
			if (BlockModel != Null) {
				declare Quad_Spawn <=> (Frame_Spawn.GetFirstChild("Quad_Spawn") as CMlQuad);
				declare Label_Spawn <=> (Frame_Spawn.GetFirstChild("Label_Spawn") as CMlLabel);
				Quad_Spawn.Image = BlockModel.Icon;
				if (L_SpawnsNames.existskey(BlockKey)) Label_Spawn.Value = L_SpawnsNames[BlockKey];
				Frame_Spawn.Visible = True;
			} else {
				Frame_Spawn.Visible = False;
			}
		} else {
			Frame_Spawn.Visible = False;
		}
	}
	
	declare Frame_Selector <=> (Page.GetFirstChild("Frame_Selector") as CMlFrame);
	if (_SpawnKey >= 0) {
		declare CursorPosX = _SpawnKey * {{{Styles::GetSize2(SpawnSize, Styles::CircleSize_Bottom()).X}}};
		Frame_Selector.Visible = True;
		LibManialink_Anim(Frame_Selector, "<frame pos=\""^CursorPosX^" 0\" />", 250, "EaseOutQuad");
	} else {
		Frame_Selector.Visible = False;
	}
	
	if (_Shift < 0) {
		Frame_Spawns.RelativePosition_V3.X = {{{-Styles::GetSize2(SpawnSize, Styles::CircleSize_Bottom()).X}}};
		LibManialink_Anim({{{Manialink::Inject("""<frame pos="0 0" id="Frame_Spawns" />""")}}}, 250, "EaseOutQuad");
	} else if (_Shift > 0) {
		Frame_Spawns.RelativePosition_V3.X = {{{Styles::GetSize2(SpawnSize, Styles::CircleSize_Bottom()).X}}};
		LibManialink_Anim({{{Manialink::Inject("""<frame pos="0 0" id="Frame_Spawns" />""")}}}, 250, "EaseOutQuad");
	}
}

main() {
	declare L_Visibility for Page = False;
	declare L_SpawnsUpdate for Page = -1;
	declare L_SpawnKey for Page = -1;
	declare L_Spawns for Page = Integer[];
	declare L_SpawnsShift for Page = 0;
	declare L_PositionY for Page = 0.;
	
	declare ClickPage = 0;
	declare NextClickUpdate = -1;
	
	declare PrevVisibility = False;
	declare PrevSpawnsUpdate = -1;
	declare PrevPositionY = -1.;
	
	LibButtons_Init();
	
	while (True) {
		yield;
		
		LibManialink_AnimLoop();
		LibButtons_Loop();
		
		if (PrevVisibility != L_Visibility) {
			PrevVisibility = L_Visibility;
			SetVisibility(L_Visibility);
		}
		
		if (PrevPositionY != L_PositionY) {
			PrevPositionY = L_PositionY;
			SetVisibility(L_Visibility);
		}
		
		if (PrevSpawnsUpdate != L_SpawnsUpdate) {
			PrevSpawnsUpdate = L_SpawnsUpdate;
			SetSpawns(L_Spawns, L_SpawnKey, L_SpawnsShift);
		}
		
		if (ClickPage != 0) {
			if (!MouseLeftButton) {
				ClickPage = 0;
				NextClickUpdate = -1;
			}
			
			if (NextClickUpdate > 0 && Now >= NextClickUpdate) {
				if (ClickPage > 0) {
					SendCustomEvent("{{{C_Event_SpawnNext}}}", Text[]);
					NextClickUpdate = Now + C_ClickTick;
				} else if (ClickPage < 0) {
					SendCustomEvent("{{{C_Event_SpawnPrev}}}", Text[]);
					NextClickUpdate = Now + C_ClickTick;
				}
			}
		}
		
		foreach (Event in PendingEvents) {
			if (Event.Type == CMlEvent::Type::MouseOver) {
				if (Event.Control != Null && Event.ControlId == "Quad_Select") {
					declare SpawnSelectKey for Event.Control = -1;
					SendCustomEvent("{{{C_Event_SpawnHover}}}", [TL::ToText(SpawnSelectKey)]);
				}
			} else if (Event.Type == CMlEvent::Type::MouseClick) {
				if (Event.Control != Null && Event.ControlId == "Quad_Select") {
					declare SpawnSelectKey for Event.Control = -1;
					SendCustomEvent("{{{C_Event_SpawnClick}}}", [TL::ToText(SpawnSelectKey)]);
				} else if (Event.ControlId == "Button_PrevSpawn") {
					SendCustomEvent("{{{C_Event_SpawnPrev}}}", Text[]);
					ClickPage = -1;
					NextClickUpdate = Now + C_InitTick;
				} else if (Event.ControlId == "Button_NextSpawn") {
					SendCustomEvent("{{{C_Event_SpawnNext}}}", Text[]);
					ClickPage = 1;
					NextClickUpdate = Now + C_InitTick;
				}
			}
		}
	}
}
--></script>
</manialink>	
""";
}

// ---------------------------------- //
// Public
// ---------------------------------- //
// ---------------------------------- //
/** Return the version number of the script
 *
 *	@return							The version number of the script
 */
Text GetScriptVersion() {
	return Version;
}

// ---------------------------------- //
/** Return the name of the script
 *
 *	@return							The name of the script
 */
Text GetScriptName() {
	return ScriptName;
}

// ---------------------------------- //
/// Get the events constants
Text Event_SpawnHover() { return C_Event_SpawnHover; }
Text Event_SpawnClick() { return C_Event_SpawnClick; }
Text Event_SpawnNext() { return C_Event_SpawnNext; }
Text Event_SpawnPrev() { return C_Event_SpawnPrev; }

// ---------------------------------- //
/** Select a spawn
 *
 *	@param	_Spawn					The spawn to select
 *	@param	_Shift					Animate the selection
 *	@param	_PlaySound				Play a sound
 */
Void SelectSpawn(Integer _Spawn, Integer _Shift, Boolean _PlaySound) {
	declare Page <=> Layers::GetPage("SpawnSelection");
	declare L_SpawnsUpdate for Page = -1;
	declare L_Spawns for Page = Integer[];
	declare L_SpawnsShift for Page = 0;
	declare L_SpawnKey for Page = -1;
	L_Spawns.clear();
	L_SpawnsShift = 0;
	L_SpawnKey = 0;
	G_Spawn = -1;
	
	declare SelectedSpawn = _Spawn;
	if (G_Spawns.count > 0 && !G_Spawns.exists(_Spawn)) SelectedSpawn = G_Spawns[0];
	
	declare SelectedSpawnKey = 0;
	foreach (SpawnKey => Spawn in G_Spawns) {
		if (Spawn == SelectedSpawn) {
			SelectedSpawnKey = SpawnKey;
			G_Spawn = Spawn;
			break;
		}
	}
	
	if (SelectedSpawnKey > G_SpawnsStart + C_SpawnsNb - 1) {
		G_SpawnsStart = SelectedSpawnKey - C_SpawnsNb + 1;
		L_SpawnsShift = 1;
	} else if (SelectedSpawnKey < G_SpawnsStart) {
		G_SpawnsStart = SelectedSpawnKey;
		L_SpawnsShift = -1;
	}
	
	if (G_SpawnsStart < 0) G_SpawnsStart = 0;
	
	L_SpawnKey = SelectedSpawnKey - G_SpawnsStart;
	
	declare Min = G_SpawnsStart - 1;
	declare Max = G_SpawnsStart + C_SpawnsNb;
	for (I, Min, Max) {
		if (G_Spawns.existskey(I)) {
			L_Spawns.add(G_Spawns[I]);
		} else {
			L_Spawns.add(-1);
		}
	}
	
	G_SpawnUpdate = Now;
	L_SpawnsUpdate = Now;
	
	if (_PlaySound) Sound::Play("EditorMenuMove");
}

// ---------------------------------- //
/// Overload SelectSpawn()
Void SelectSpawn(Integer _Spawn, Integer _Shift) {
	SelectSpawn(_Spawn, _Shift, False);
}

// ---------------------------------- //
/// Overload SelectSpawn()
Void SelectSpawn(Integer _Spawn, Boolean _PlaySound) {
	SelectSpawn(_Spawn, 0, _PlaySound);
}

// ---------------------------------- //
/** Select a spawn
 *
 *	@param	_Spawn					The spawn block to select
 */
Void SelectSpawn(Integer _Spawn) {
	SelectSpawn(_Spawn, 0);
}

// ---------------------------------- //
/// Select the next spaw
Void NextSpawn() {
	declare Key = G_Spawns.keyof(G_Spawn);
	if (Key >= 0) {
		Key += 1;
		if (Key > G_Spawns.count - 1) Key = 0;
		SelectSpawn(G_Spawns[Key], 1);
	} else {
		SelectSpawn(-1);
	}
	Sound::Play("EditorMenuMove");
}

// ---------------------------------- //
/// Select the previous spawn
Void PrevSpawn() {
	declare Key = G_Spawns.keyof(G_Spawn);
	if (Key >= 0) {
		Key -= 1;
		if (Key < 0) Key = G_Spawns.count - 1;
		SelectSpawn(G_Spawns[Key], -1);
	} else {
		SelectSpawn(-1);
	}
	Sound::Play("EditorMenuMove");
}

// ---------------------------------- //
/** Get the spawn last update time
 *
 *	@return							The spawn update time
 */
Integer GetSpawnUpdate() {
	return G_SpawnUpdate;
}

// ---------------------------------- //
/** Get the currently selected spawn
 *
 *	@return							The selected spawn
 */
CBlock GetSpawn() {
	if (Blocks.existskey(G_Spawn)) return Blocks[G_Spawn];
	return Null;
}

// ---------------------------------- //
/** Set the spawns to display
 *
 *	@param	_Spawns					The spawns blocks
 */
Void SetSpawns(Integer[] _Spawns) {
	declare Page <=> Layers::GetPage("SpawnSelection");
	declare L_SpawnsNames for Page = Text[Integer];
	L_SpawnsNames.clear();
	G_Spawns.clear();
	
	declare StartsCount = 0;
	declare CheckpointsCount = 0;
	foreach (SpawnKey in _Spawns) {
		declare CBlock Spawn;
		if (Blocks.existskey(SpawnKey)) Spawn <=> Blocks[SpawnKey];
		
		if (Spawn == Null) continue;
		if (Spawn.BlockModel.WaypointType == CBlockModel::EWayPointType::Start || Spawn.BlockModel.WaypointType == CBlockModel::EWayPointType::StartFinish) {
			StartsCount += 1;
			//L16N [MapEditor] A starting line block
			L_SpawnsNames[SpawnKey] = _("|BlockInfo|Start");
		} else if (Spawn.BlockModel.WaypointType == CBlockModel::EWayPointType::Checkpoint) {
			CheckpointsCount += 1;
			//L16N [MapEditor] A checkpoint block
			L_SpawnsNames[SpawnKey] = TL::Compose("%1 #%2", _("|BlockInfo|Checkpoint"), TL::ToText(CheckpointsCount));
		}
		
		G_Spawns.add(SpawnKey);
	}
	
	SelectSpawn(G_Spawn);
}

// ---------------------------------- //
/** Set the position of the UI
 *
 *	@param	_PositionY				The position
 */
Void SetPositionY(Real _PositionY) {
	declare Page <=> Layers::Get("SpawnSelection").LocalPage;
	declare L_PositionY for Page = 0.;
	L_PositionY = _PositionY;
}

// ---------------------------------- //
/** Set the spawn selection visibility
 *
 *	@param	_Visible				Show the spawn selection if True
 */
Void SetVisibility(Boolean _Visible) {
	declare Page <=> Layers::GetPage("SpawnSelection");
	declare L_Visibility for Page = False;
	L_Visibility = _Visible;
}

// ---------------------------------- //
/// Show the spawn selection
Void Show() {
	SetVisibility(True);
}

// ---------------------------------- //
/// Hide the spawn selection
Void Hide() {
	SetVisibility(False);
}

// ---------------------------------- //
/// Unload the library
Void Unload() {
	Layers::Destroy("SpawnSelection");
	//Layers::Unload();
}

// ---------------------------------- //
/// Load the library
Void Load() {
	Unload();
	
	//Layers::Load();
	Buttons::Load();
	declare LayerSpawnSelection <=> Layers::Create("SpawnSelection");
	LayerSpawnSelection.ManialinkPage = Private_GetSpawnSelectionML();
	Buttons::Unload();
	
	G_Spawns.clear();
	G_SpawnsStart = 0;
	G_SpawnUpdate = -1;
	G_Spawn = -1;
	
	declare Page <=> LayerSpawnSelection.LocalPage;
	declare L_SpawnsUpdate for Page = -1;
	declare L_SpawnKey for Page = -1;
	declare L_Spawns for Page = Integer[];
	declare L_SpawnsNames for Page = Text[Integer];
	declare L_SpawnsShift for Page = 0;
	declare L_Visibility for Page = False;
	declare L_PositionY for Page = 0.;
	L_SpawnsUpdate = -1;
	L_SpawnKey = -1;
	L_Spawns.clear();
	L_SpawnsNames.clear();
	L_SpawnsShift = 0;
	L_Visibility = False;
	L_PositionY = Styles::GetSize2(<0., 9.>, Styles::CircleSize_Bottom()).Y - 90.;
}