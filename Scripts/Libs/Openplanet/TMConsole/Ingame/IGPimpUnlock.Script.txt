// ATTENTION !
// ce script presuppose une connaissance *a priori* de ce qui est contenu dans Menus/PimpUnlock.Sctipt.txt
// par exemple, on s'attend  avoir un nouvel unlock toutes les 10 golds dans la campagne solo.

#Const TMC_MEDAL_AUTHOR		0
#Const TMC_MEDAL_GOLD		1


/// on gagne toujours quelque chose avec les chelons.
Boolean NewStuffWithEchelon(Integer _NewEchelon)  {
	return True;
}

/**
 * à appeler AVANT de donner la médaille.
 * 
 * @_MedalType peut prendre les valeurs
 * 0 : Trackmaster	= TMC_MEDAL_AUTHOR
 * 1 : Gold			= TMC_MEDAL_GOLD
 * 2 : Silver		= TMC_MEDAL_SILVER
 * 3 : Bronze		= TMC_MEDAL_BRONZE
 * 
 * @_Campaign peut prendre les valeurs
 *  0 : Solo campaign
 *  1 : Super Solo campaign
 *  2 : Double Driver campaign
 **/
Boolean NewStuffWithMedal(CUser _User, Integer _PrevMedalType, Integer _NewMedalType, Integer _Campaign) 
{	
	// il n'y a que Gold et Trackmaster qui donnent des recompenses.
	if(_NewMedalType > TMC_MEDAL_GOLD) return False;
	
	// la deuxieme condition est en trop, mais laissee pour symetrie
	declare Boolean HasNewGold 			= (_PrevMedalType > TMC_MEDAL_GOLD) && (_NewMedalType <= TMC_MEDAL_GOLD); 
	declare Boolean HasNewTrackmaster 	= (_PrevMedalType > TMC_MEDAL_AUTHOR) && (_NewMedalType <= TMC_MEDAL_AUTHOR);

	declare Integer SoloGoldMedals		for _User;
	declare Integer DDGoldMedals		for _User;
	
	switch(_Campaign) {
		// Solo campaign
		case 0:{
			if(HasNewGold) {
				if ((SoloGoldMedals >= DDGoldMedals) && 
				// EG : les règles... bon, ok, c'est moche. tant pis, fin de prod.
				(
					(SoloGoldMedals == 0) || (SoloGoldMedals == 2) || (SoloGoldMedals == 4) ||
					(SoloGoldMedals < 50 && (SoloGoldMedals % 5) == 4) ||
					(SoloGoldMedals == 59) ||
					((SoloGoldMedals % 20) == 19)
				)) return True;
			} 
			
			if(HasNewTrackmaster) {
				declare Integer SoloTMMedals		for _User;
				if((SoloTMMedals == 0) || (SoloTMMedals == 199)) return True;	// la prochaine fait 1 ou 200
			}
		}
		
		// Super Solo campaign
		case 1:{
			if(HasNewGold) {
				declare Integer SuperSoloGoldMedals	for _User;
				if (SuperSoloGoldMedals == 199)	return True; 						// la prochaine fait 200
			}
			
			if(HasNewTrackmaster) {
				declare Integer SuperSoloTMMedals	for _User;
				if (SuperSoloTMMedals == 199)	return True; 						// la prochaine fait 200
			}
		}
		
		// Double Driver campaign
		case 2:{
			if(HasNewGold) {
				if ((DDGoldMedals >= SoloGoldMedals) && 
				// EG : les règles... bon, ok, c'est moche. tant pis, fin de prod.
				(
					(DDGoldMedals == 0) || (DDGoldMedals == 2) || (DDGoldMedals == 4) ||
					(DDGoldMedals < 50 && (DDGoldMedals % 5) == 4) ||
					(DDGoldMedals == 59) ||
					((DDGoldMedals % 20) == 19)
				)) return True;
			} 
			
			if(HasNewTrackmaster) {
				declare Integer DDTMMedals			for _User;
				if (DDTMMedals == 199)			return True;							// la prochaine fait 200
			}
		}
	}
	
	return False;
}
